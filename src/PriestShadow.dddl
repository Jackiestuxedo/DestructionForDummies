class: priest
build: shadow

var haveMastery;

PreCombat {
    haveMastery = Known("Mastery");
}

// no way to regain mana, so just shoot if OOM
InCombat ("Shoot") {
    clip {
	"Vampiric Touch": 	1;
	"Shadow Word: Pain":	1;
	"Devouring Plague":	1;
    };

    // our bubble and armor are *really* useful when solo fighting, but
    // less so in a group
    if (Solo()) {
        alternate {
	    "Power Word: Shield":	!Buff() && Castable();
	    "Inner Fire":		!Buff();
	};
    }

    var EvTime, EvCount;
    EvTime, EvCount = BuffInfo("Evangelism");

    alternate (1) { "Shadowfiend": Castable(); };
    alternate (2) { "Archangel":   EvCount >= 5; };

    "Shadowform":		!Buff();
    "Vampiric Embrace":		!Buff();

    "Vampiric Touch": 		!Debuff();
    "Shadow Word: Pain":	!Debuff();
    "Devouring Plague":		!Debuff();
    
    /*
     *  Don't mindblast if we will need to refresh (or apply) Empowered Shadows
     *  and we have no orbs (unless we don't have Mastery yet, in which case
     *  we should just be Mind Blasting on cooldown)
     */
    "Mind Blast":		(!haveMastery || Buff("Shadow Orb") ||
				 BuffTime("Empowered Shadow")  > 6.5);

    "Shadow Word: Death":	TargetHealth() <= 25;
    "Mind Flay";
}

OutOfCombat {
    clip {
	"Power Word: Fortitude": 300;
	"Inner Fire":		 300;
	"Vampiric Embrace":	 300;
    };
    	
    if (!Mounted()) {
        "Power Word: Fortitude":!Buff();
    	"Inner Fire":		!Buff();
	"Vampiric Embrace":	!Buff();
        "Power Word: Shield":	!Buff() && Castable() && Solo();
    }
}
